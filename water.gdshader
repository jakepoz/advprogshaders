shader_type canvas_item;

uniform sampler2D noise1 : repeat_enable;
uniform sampler2D noise2 : repeat_enable;

uniform vec2 scroll1 = vec2(0.05, 0.05);
uniform vec2 scroll2 = vec2(-0.05, -0.05);

uniform sampler2D screen_texture : hint_screen_texture, repeat_disable, filter_nearest;
uniform float distortion_strength : hint_range(-1, 1) = 0.01;

uniform vec4 tone : source_color;

uniform vec4 top_color : source_color;
uniform float top_light_start : hint_range(0.0, 1.0) = 0.275;
uniform float top_light_end : hint_range(0.0, 1.0) = 0.4;


void fragment() {
	float depth = texture(noise1, UV + scroll1 * TIME).r + texture(noise2, UV + scroll2 * TIME * 0.2).r;
	vec4 screen_col = texture(screen_texture, SCREEN_UV + distortion_strength * vec2(0.0, -depth));
	
	vec4 top_light = smoothstep(top_light_start, top_light_end, depth) * top_color;
	
	COLOR = screen_col * tone + top_light;
	COLOR.a = 1.0;
}
